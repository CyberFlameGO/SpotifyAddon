/*
 *     SpotifyAddon is an addon for MediaMod which adds Spotify Support
 *     Copyright (C) 2021 Conor Byrne
 *
 *     This program is free software: you can redistribute it and/or modify
 *     it under the terms of the GNU General Public License as published by
 *     the Free Software Foundation, either version 3 of the License, or
 *     (at your option) any later version.
 *
 *     This program is distributed in the hope that it will be useful,
 *     but WITHOUT ANY WARRANTY; without even the implied warranty of
 *     MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *     GNU General Public License for more details.
 *
 *     You should have received a copy of the GNU General Public License
 *     along with this program.  If not, see <https://www.gnu.org/licenses/>.
 */

buildscript {
    repositories {
        maven { url "https://jitpack.io" }
    }

    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:4.0.4"
        classpath "com.github.ReplayMod:ForgeGradle:fab6116:all"
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.4.31'
    id 'com.github.johnrengelman.shadow' version '4.0.4'
}

apply plugin: 'net.minecraftforge.gradle.forge'

group 'com.mediamod.addons.spotify'
version '1.0-SNAPSHOT'
sourceCompatibility = targetCompatibility = "1.8"

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"
    mappings = "stable_22"
    makeObfSourceJar = true
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

configurations {
    embed
    compile.extendsFrom(embed)
}

dependencies {
    // Don't remove these, they are required by MediaMod!
    compileOnly 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.1.0'
    compileOnly ('com.github.MediaModMC:MediaMod:rewrite-SNAPSHOT') {
        changing = true
    }

    embed("com.github.jkcclemens:khttp:-SNAPSHOT")
    embed('com.github.dreamhopping:Kotify:main-SNAPSHOT') {
        changing = true
    }

    // If you wish to include a dependency in your jar use the "embed" method
    // embed 'com.example:ExampleDependency:1.0-SNAPSHOT'
}

shadowJar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    configurations = [project.configurations.embed]

    /**
     * It is not recommended to change these relocation settings, if you do your addon may not work!
     */
    relocate "kotlin", "mediamod.kotlin"
    relocate "kotlinx", "mediamod.kotlinx"
}

configurations.all {
    // Check for updates every build
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

tasks.build.finalizedBy tasks.shadowJar
