/*
 *     SpotifyAddon is an addon for MediaMod which adds Spotify Support
 *     Copyright (C) 2021 Conor Byrne
 *
 *     This program is free software: you can redistribute it and/or modify
 *     it under the terms of the GNU General Public License as published by
 *     the Free Software Foundation, either version 3 of the License, or
 *     (at your option) any later version.
 *
 *     This program is distributed in the hope that it will be useful,
 *     but WITHOUT ANY WARRANTY; without even the implied warranty of
 *     MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *     GNU General Public License for more details.
 *
 *     You should have received a copy of the GNU General Public License
 *     along with this program.  If not, see <https://www.gnu.org/licenses/>.
 */

buildscript {
    repositories {
        maven { url "https://maven.minecraftforge.net" }
        maven { url "https://jitpack.io" }
    }

    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:5.2.0"
        classpath "com.github.asbyth:ForgeGradle:da0302a"
    }
}

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.5.10"
    id "com.github.johnrengelman.shadow" version "5.2.0"
}

apply plugin: "net.minecraftforge.gradle.forge"

group "com.mediamod.addons.spotify"
version "1.0-SNAPSHOT"
sourceCompatibility = targetCompatibility = "1.8"

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"
    mappings = "stable_22"
    makeObfSourceJar = true
}

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
    maven { url "https://repo.sk1er.club/repository/maven-public" }
}

configurations {
    embed
    compile.extendsFrom(embed)
}

dependencies {
    // Gson is included in Minecraft, the addon uses this for parsing responses from Spotify
    compileOnly "com.google.code.gson:gson:2.8.6"
    compileOnly "com.github.MediaModMC.MediaMod:core:750a4716cd"
    compileOnly "gg.essential:Vigilance:107-10809-SNAPSHOT"

    embed("com.github.jkcclemens:khttp:39f76b4") {
        exclude group: "org.jetbrains.kotlin"
    }

    embed("com.github.cbyrneee:Kotify:ff311f0") {
        exclude group: "org.jetbrains.kotlin"
    }

    // If you wish to include a dependency in your jar use the "embed" method
    // embed "com.example:ExampleDependency:1.0-SNAPSHOT"
}

shadowJar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    configurations = [project.configurations.embed]
}

tasks.build.finalizedBy tasks.shadowJar
